plugins {
	id "com.devsoap.plugin.vaadin" version "1.3.1"
}

allprojects {
	apply plugin: "eclipse-wtp"
	apply plugin: "idea"
}

//common for both projects
subprojects {
	ext.baseNameUpper = project.parent.name.capitalize()
	ext.baseNameLower = project.parent.name.toLowerCase()

	apply plugin: "com.devsoap.plugin.vaadin"

	vaadin.version="8.3.1"

	vaadinCompile.draftCompile = true
	vaadinCompile.strict = true
	vaadinCompile.manageWidgetset = false
	vaadin.logToConsole = true

	vaadinAddon.author = "Risto Yrjänä / Vaadin, Federico Russo"
	vaadinAddon.license = "Apache License 2.0"
	vaadinAddon.title = baseNameUpper

	sourceCompatibility = "1.8"
	targetCompatibility = "1.8"

	group = "org.vaadin.risto.${baseNameLower}"

	//creates archives named "name-version.jar" and "name.war" where name is the name of the main project
	archivesBaseName = baseNameLower
}

//addon project, only creates the Directory-compatible JAR
project(":addon") {
	eclipse.project.name = "${baseNameUpper}-addon"

	version = "3.0.0"
	//vaadin.widgetset = "${group}.Widgetset"

	dependencies {
		compile "org.eclipse.jetty:jetty-jsp:9.3.0.M1"
		testCompile "junit:junit:latest.release"
	}

	jar {
		from sourceSets.main.allJava //include sources
	}

	war.enabled = false //war plugin applied by the Vaadin plugin, not needed here
}

//demo project, builds addon and packages demo UI as WAR
project(":demo") {

	eclipse.project.name = "${baseNameUpper}-demo"

	dependencies {
		compile project(":addon")
		compile "com.google.guava:guava:latest.release"
		compile "org.apache.commons:commons-lang3:3.5"
	}

	vaadinSuperDevMode.openInBrowser = false
	vaadinRun.openInBrowser = true
	vaadinRun.server = 'jetty'
}

task(dist, type: Sync, dependsOn: [':addon:jar', ':demo:war']) {
	description = "Copies the addon JAR and the demo WAR to target/"
	File targetDir = mkdir("target")
	from project(":addon").jar.archivePath
	from project(":demo").war.archivePath
	into targetDir
}
